import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.android.build.gradle.internal.tasks.L8DexDesugarLibTask

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: "com.github.johnrengelman.shadow"


def CORE_VERSION = "2.6.1"
def CONSOLE_VERSION = "2.6.1"
def LUAMIRAI_VERSION = "2.0.8"

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "io.github.mzdluo123.mirai.android"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 48
        versionName "3.1.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "COREVERSION", "\"$CORE_VERSION\"")
        buildConfigField("String", "CONSOLEVERSION", "\"$CONSOLE_VERSION\"")
        buildConfigField("String", "LUAMIRAI_VERSION", "\"$LUAMIRAI_VERSION\"")
    }
    buildFeatures {
        dataBinding = true
    }


    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
        exclude 'silk4j_libs/windows-shared-x64/*'
        exclude 'silk4j_libs/windows-shared-x86/*'
        exclude 'silk4j_libs/macos-x64/*'
        exclude 'silk4j_libs/filelist.txt'
    }

    lintOptions {
        abortOnError false
    }


    testOptions {
        animationsDisabled = true
    }

    configurations {
        nnio
    }

    tasks.withType(L8DexDesugarLibTask) {
        if (it.name.contains("Release"))
        {
            keepRulesFiles.from("desugar-rules.pro")
        }
    }
}

kotlin.sourceSets.all {
    languageSettings.useExperimentalAnnotation("kotlin.RequiresOptIn")
    languageSettings.useExperimentalAnnotation("kotlin.ExperimentalUnsignedTypes")
    languageSettings.useExperimentalAnnotation("net.mamoe.mirai.console.ConsoleFrontEndImplementation")
    languageSettings.useExperimentalAnnotation("splitties.experimental.ExperimentalSplittiesApi")
}

task nnioJar (type: ShadowJar) {
    getArchiveClassifier().set('nnio')

    relocate 'org.lukhnos.nnio.file', 'java.nio.file'
    relocate 'org.lukhnos.nnio.channels', 'java.nio.channels'

    configurations = [project.configurations.nnio]
}

dependencies {
    implementation (fileTree(dir: 'libs', include: ['*.jar']))
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.core:core-ktx:1.5.0-rc01'

    //androidx-appcompat
    implementation 'androidx.appcompat:appcompat:1.2.0'

    //androidx-legacy
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //androidx-constraintlayout
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    //androidx-navigation
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    //androidx-lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    //androidx-preference
    implementation 'androidx.preference:preference:1.1.1'

    //kotlinx-coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"


    //zip
    implementation 'net.lingala.zip4j:zip4j:2.5.2'

    //BaseRecyclerViewAdapterHelper
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.2'

    //mirai-core
    implementation("net.mamoe:mirai-core:$CORE_VERSION")
//    implementation("net.mamoe:mirai-core:$CORE_VERSION"){
//        exclude  module: "mirai-core-utils"
//         // 修复Duplicate class
//    }

    //mirai-lua
    implementation "com.ooooonly:luaMirai:${LUAMIRAI_VERSION}"
   // implementation 'org.json:json:20160212'
    implementation 'com.ooooonly:giteeman:0.1.1'

    //splitties
    implementation("com.louiscad.splitties:splitties-fun-pack-android-base:3.0.0-alpha06")
    implementation("com.louiscad.splitties:splitties-fun-pack-android-appcompat:3.0.0-alpha06")

    //acra
    implementation "ch.acra:acra-core:5.1.3"
    implementation "ch.acra:acra-dialog:5.1.3"

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //yaml
    implementation 'net.mamoe.yamlkt:yamlkt:0.7.5'
    implementation group: 'com.moandjiezana.toml', name: 'toml4j', version: '0.7.2'

    //okhttp3
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    //test
    implementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'

    implementation 'com.fanjun:keeplive:1.1.22'

    // fuck!! 他不能在Android平台工作
    //    implementation  'org.codehaus.groovy:groovy:2.4.6:grooid'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'


    debugImplementation 'androidx.fragment:fragment-testing:1.2.4'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    //  https://mvnrepository.com/artifact/net.mamoe/mirai-console
    implementation group: 'net.mamoe', name: 'mirai-console', version: CONSOLE_VERSION

    nnio 'com.github.rtm516:nnio:c7b291f4ca'
    implementation tasks.nnioJar.outputs.files

}
